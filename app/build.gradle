/*
 * Copyright (c) 2016. Saiy Ltd. All Rights Reserved.
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Affero General Public License as published
 * by the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Affero General Public License for more details.
 *
 * You should have received a copy of the GNU Affero General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */
plugins {
    alias(libs.plugins.androidApplication)
    alias(libs.plugins.protobuf.gradle.plugin)
    alias(libs.plugins.hilt)
    alias(libs.plugins.googleServices)
}

android {
    namespace 'ai.saiy.android'
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId "assistant.saiy.android"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 3
        versionName "1.0.3"
        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }
        resourceConfigurations = ["en", "zh-rCN"]
        // Required for desugar_jdk_libs when setting minSdkVersion to 20 or lower
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
    }

    compileOptions {
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled = true // <- this flag is required
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
        incremental true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        buildConfig = true
    }
    packagingOptions {
        jniLibs {
            excludes += ['/META-INF/androidx/**',
                         '/META-INF/license/**',
                         '/META-INF/native/**',
                         '/META-INF/native-image/**',
                         'com/**',
                         'org/**']
        }
        resources {
            excludes += ['**/*.kotlin_builtins',
                         '**/*.kotlin_module',
                         '**/DebugProbesKt.bin',
                         '/META-INF/androidx/**',
                         '/META-INF/DEPENDENCIES',
                         '/META-INF/license/**',
                         '/META-INF/native/**',
                         '/META-INF/native-image/**',
                         '/META-INF/NOTICE.md',
                         '/META-INF/INDEX.LIST',
                         'com/**',
                         'org/**',
                         '**/*.java',
                         '**/*.proto',
                         'androidsupportmultidexversion.txt']
        }
    }
}

configurations {
    implementation.exclude group: "org.apache.httpcomponents", module: "httpclient"
}

dependencies {
    //Duplicate class kotlin-stblib vs kotlin-stdlib-jdk7/8
    constraints {
        add("implementation", "org.jetbrains.kotlin:kotlin-stdlib-jdk7") {
            version { require("1.9.24") }
        }
        add("implementation", "org.jetbrains.kotlin:kotlin-stdlib-jdk8") {
            version { require("1.9.24") }
        }
    }

    implementation fileTree(include: ['*.aar', '*.jar', '*.so'], dir: 'libs')
    implementation libs.multidex // Required for desugar_jdk_libs
    coreLibraryDesugaring libs.desugar.jdk.libs
	implementation libs.rxjava3
    implementation libs.rx.android
    implementation libs.rxlint

    implementation libs.material
    implementation libs.appcompat
    implementation(libs.activity) {
        exclude group: 'androidx.activity', module: 'activity-ktx'
    }
    implementation libs.annotation
    implementation libs.browser
    implementation libs.cardview
    implementation libs.constraintlayout
    implementation libs.customview
    implementation libs.collection
    implementation (libs.core) {
        exclude group: 'androidx.core', module: 'core-ktx'
    }
    implementation libs.documentfile
    implementation libs.drawerlayout
    implementation(libs.fragment) {
        exclude group: 'androidx.core', module: 'core-ktx'
        exclude group: 'androidx.fragment', module: 'fragment-ktx'
    }
    implementation libs.core.runtime
    implementation(libs.lifecycle.runtime) {
        exclude group: 'androidx.core', module: 'core-ktx'
        exclude group: 'androidx.lifecycle', module: 'lifecycle-runtime-ktx'
    }
    implementation(libs.lifecycle.livedata) {
        exclude group: 'androidx.lifecycle', module: 'lifecycle-livedata-core-ktx'
    }
    implementation(libs.lifecycle.viewmodel) {
        exclude group: 'androidx.lifecycle', module: 'lifecycle-viewmodel-ktx'
    }
    implementation(libs.lifecycle.viewmodel.savedstate) {
        exclude group: 'androidx.core', module: 'core-ktx'
    }
    annotationProcessor libs.common.java8
    implementation libs.recyclerview
    implementation libs.print
    implementation libs.transition
    implementation libs.work.runtime
    implementation(libs.hilt.android) {
        exclude group: 'androidx.fragment', module: 'fragment'
        exclude group: 'androidx.fragment', module: 'fragment-ktx'
        exclude group: 'androidx.activity', module: 'activity'
        exclude group: 'androidx.activity', module: 'activity-ktx'
    }

    implementation(libs.google.cloud.translate) {
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
        exclude group: 'com.google.guava'
    }
    implementation libs.play.services.base
    implementation libs.play.services.basement
    implementation libs.play.services.auth
    implementation libs.play.services.location
    implementation libs.play.services.ads.identifier
    implementation libs.play.services.ads
    implementation libs.play.services.tasks
    implementation libs.play.services.wearable
    implementation libs.speechkit
    implementation libs.guava
    // https://github.com/google/gson
    implementation libs.gson
    // https://github.com/googleapis/google-http-java-client
    implementation libs.google.http.client.gson
    implementation libs.speechrecognition
    implementation libs.billing
    implementation libs.commons.lang3
    implementation libs.commons.codec
    implementation libs.commons.io
    implementation libs.commons.fileupload
    implementation libs.commons.text
    implementation (libs.commons.validator) {
        //noinspection DuplicatePlatformClasses, it's OK to be excluded
        exclude group: 'commons-logging', module: 'commons-logging'
    }
    implementation libs.simmetrics.core
    // https://github.com/googleapis/java-cloud-bom
    implementation(platform(libs.libraries.bom)) {
        exclude group: 'com.google.protobuf', module: 'protobuf-java'
        exclude group: 'com.google.protobuf', module: 'protobuf-java-util'
    }
    // https://github.com/dialogflow/dialogflow-android-client
    // https://github.com/googleapis/google-cloud-java/tree/main/java-dialogflow
    implementation(libs.cloud.dialogflow) {
        //noinspection DuplicatePlatformClasses, it's OK to be excluded
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'io.grpc', module: 'grpc-protobuf'
        exclude group: 'com.google.protobuf', module: 'protobuf-java-lite'
//        exclude group: 'com.google.protobuf', module: 'protobuf-java'
//        exclude group: 'com.google.protobuf', module: 'protobuf-java-util'
    }
    implementation(libs.proto.google.common) {
        exclude group: 'com.google.protobuf', module: 'protobuf-java'
    }
    // Add the dependency for the Firebase Authentication library
    implementation(platform(libs.firebase.bom)) {
        exclude group: 'com.google.firebase', module: 'firebase-common-ktx'
    }
    implementation(libs.firebase.appcheck) {
        exclude group: 'com.google.firebase', module: 'firebase-common-ktx'
    }
    implementation(libs.firebase.appcheck.playintegrity) {
        exclude group: 'com.google.firebase', module: 'firebase-appcheck'
        exclude group: 'com.google.firebase', module: 'firebase-common-ktx'
    }
    implementation libs.firebase.analytics
    implementation(libs.firebase.auth) {
        exclude group: 'com.google.firebase', module: 'firebase-auth-ktx'
    }
    implementation(libs.firebase.functions) {
        exclude group: 'com.google.firebase', module: 'firebase-common-ktx'
    }
    implementation libs.firebase.messaging
    implementation libs.firebase.database
    implementation libs.firebase.ui.auth
    // https://github.com/TooTallNate/Java-WebSocket
    implementation libs.java.web.socket
    implementation libs.licensesdialog
    implementation(libs.simple.xml) {
        exclude group: 'xpp3', module: 'xpp3'
        exclude group: 'stax', module: 'stax-api'
        exclude group: 'stax', module: 'stax'
    }
    implementation(libs.grpc.okhttp) {
        exclude module: 'jsr305'
    }
    implementation(libs.grpc.protobuf) {
        exclude module: 'jsr305'
    }
    implementation(libs.grpc.stub) {
        exclude module: 'jsr305'
    }
    implementation(libs.grpc.auth) {
        exclude module: 'jsr305'
    }
    implementation(libs.protobuf.java)
    implementation(libs.protobuf.java.util)
    implementation(libs.javax.annotation.api) {
        exclude module: 'annotation'
    }
    // https://github.com/googleapis/google-auth-library-java
    implementation(libs.google.auth.library.oauth2.http) {
        exclude group: 'org.apache.httpcomponents'
        exclude group: 'com.google.guava'
        exclude module: 'jsr305'
        exclude module: 'httpclient'
    }
    implementation (libs.volley)
    implementation (libs.java.aes.crypto)
    implementation (libs.twitter4j)
    implementation (libs.facebook.login) {
        exclude group: 'androidx.core', module: 'core-ktx'
    }
    implementation (libs.facebook.share) {
        exclude group: 'androidx.core', module: 'core-ktx'
    }
    // https://github.com/cmusphinx/pocketsphinx-android
    implementation project(':pocketsphinx-android-5prealpha-nolib')
    implementation project(':ToastCompat')
    implementation project(':lib')
    implementation project(':StandOut')
    wearApp project(':Saiy-Wear')

    annotationProcessor libs.hilt.compiler
    testImplementation libs.junit
}

// https://github.com/google/protobuf-gradle-plugin
// https://github.com/grpc/grpc-java
protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:4.33.5'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.65.1'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {}
            }
            task.plugins {
                grpc {}
            }
        }
    }
}

ant.importBuild('assets.xml')
preBuild.dependsOn(list, checksum)
clean.dependsOn(clean_assets)
